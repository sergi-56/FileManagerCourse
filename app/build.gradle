plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.course'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.course.filemanager.Main'
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

test {
    useJUnitPlatform()
}

javafx {
    version = '21.0.4'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass.get())
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

javadoc {
    options.encoding = 'UTF-8'
}
// --- JavaFX GUI (отдельный запуск, не ломает консольный run) ---
tasks.register("runFx", JavaExec) {
    group = "application"
    description = "Run JavaFX GUI version"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.course.filemanager.FileManagerFxApp")
}

